# Maintainer: rw-r-r-0644 <rwrr0644@gmail.com>
# Contributors: WinterMute <davem@devkitpro.org>, Elouan Martinet <exa@elou.world>

pkgname=wii-curl
pkgver=7.75.0
pkgrel=1
pkgdesc='Library for transferring data with URLs. (for Nintendo Wii homebrew development)'
arch=('any')
url='https://curl.haxx.se'
license=('MIT')
options=(!strip libtool staticlibs)
depends=('ppc-zlib' 'libwiisocket' 'wii-mbedtls')
makedepends=('wii-pkg-config')

source=("https://curl.haxx.se/download/curl-${pkgver}.tar.xz")
sha256sums=('fe0c49d8468249000bda75bcfdf9e30ff7e9a86d35f1a21f428d79c389d55675')

build() {
  cd curl-$pkgver

  unset CFLAGS CPPFLAGS CXXFLAGS LDFLAGS
  export DEVKITPRO=/opt/devkitpro
  export DEVKITPPC=$DEVKITPRO/devkitPPC
  export PORTLIBS_PREFIX=$DEVKITPRO/portlibs/wii
  
  export MACHDEP="-DGEKKO -mrvl -mcpu=750 -meabi -mhard-float"
  export LDFLAGS="${MACHDEP} ${LDFLAGS} -L${DEVKITPRO}/libogc/lib/wii/lib -L${DEVKITPRO}/portlibs/wii/lib -L${DEVKITPRO}/portlibs/ppc/lib"
  export CPPFLAGS="${MACHDEP} ${CPPFLAGS} -I${DEVKITPRO}/libogc/include"


  ./configure \
    CFLAGS="${CPPFLAGS} -Wl,-L${DEVKITPRO}/portlibs/wii/lib -Wl,-L${DEVKITPRO}/portlibs/ppc/lib -I${DEVKITPRO}/portlibs/ppc/include -I${DEVKITPRO}/portlibs/wii/include -I${DEVKITPRO}/libogc/include -L${DEVKITPRO}/libogc/lib/wii" \
    CPPFLAGS="${CPPFLAGS}" \
    LDFLAGS="${LDFLAGS}" \
    LIBS="-lmbedtls -lmbedcrypto -lmbedx509 -lwiisocket -logc" \
    --prefix=${PORTLIBS_PREFIX} --host=powerpc-eabi --target=powerpc-eabi \
    --disable-shared --enable-static --disable-ipv6 --disable-unix-sockets \
    --disable-manual --disable-ntlm-wb --disable-threaded-resolver --disable-socketpair \
    --with-mbedtls=$PORTLIBS_PREFIX

  make -C lib
}

package() {
  export DEVKITPRO=/opt/devkitpro
  export DEVKITPPC=$DEVKITPRO/devkitPPC
  export PORTLIBS_PREFIX=$DEVKITPRO/portlibs/wii
  export PATH=$DEVKITPPC/bin/:$PATH
  cd curl-$pkgver

  make DESTDIR="$pkgdir" -C lib install
  make DESTDIR="$pkgdir" -C include install
  make DESTDIR="$pkgdir" install-binSCRIPTS

  install -Dm644 COPYING "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING
}
