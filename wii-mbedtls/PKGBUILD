# Maintainer: rw-r-r-0644 <rwrr0644@gmail.com>
# Contributors: WinterMute <davem@devkitpro.org>, Elouan Martinet <exa@elou.world>

pkgname=wii-mbedtls
pkgver=2.28.7
pkgrel=1
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library (for Nintendo Wii homebrew development)'
arch=('any')
url='https://tls.mbed.org/'
license=('apache')
options=(!strip libtool staticlibs)
depends=(ppc-zlib)
makedepends=('wii-cmake' 'wii-pkg-config' 'libwiisocket')
source=( "https://github.com/Mbed-TLS/mbedtls/archive/refs/tags/mbedtls-${pkgver}.tar.gz"
         'wii-mbedtls.patch' )

sha256sums=('4390bc4ab1ea9a1ddf3725f540d0f80838c656d1d7987a1cee8b4da43e4571de'
            'SKIP')

build() {
  cd mbedtls-mbedtls-$pkgver

  patch -Np1 -i $srcdir/wii-mbedtls.patch

  unset CFLAGS CPPFLAGS CXXFLAGS LDFLAGS

  ./scripts/config.pl set MBEDTLS_NO_PLATFORM_ENTROPY
  ./scripts/config.pl set MBEDTLS_TIMING_C
  ./scripts/config.pl set MBEDTLS_HAVEGE_C
  ./scripts/config.pl set MBEDTLS_CMAC_C
  ./scripts/config.pl unset MBEDTLS_ENTROPY_HARDWARE_ALT
  ./scripts/config.pl unset MBEDTLS_SELF_TEST
  ./scripts/config.pl unset MBEDTLS_TIMING_ALT

  export MACHDEP="-DGEKKO -mrvl -mcpu=750 -meabi -mhard-float"
  export LDFLAGS="${MACHDEP} ${LDFLAGS} -L${DEVKITPRO}/libogc/lib/wii"
  export CPPFLAGS="${MACHDEP} ${CPPFLAGS} -I${DEVKITPRO}/libogc/include"

  cmake -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/cmake/Wii.cmake \
    -DCMAKE_INSTALL_PREFIX="${DEVKITPRO}/portlibs/wii" \
    -DCMAKE_C_FLAGS="${CPPFLAGS}" \
    -DCMAKE_CXX_FLAGS="${CPPFLAGS}" \
    -DZLIB_ROOT="${DEVKITPRO}/portlibs/ppc" \
    -DENABLE_ZLIB_SUPPORT=TRUE -DENABLE_TESTING=FALSE -DENABLE_PROGRAMS=FALSE \
    .

  make

}

package() {

  cd mbedtls-mbedtls-$pkgver

  make install DESTDIR="$pkgdir"

  install -d "$pkgdir"$PORTLIBS_PREFIX/licenses/$pkgname
  cp -v LICENSE "$pkgdir"$PORTLIBS_PREFIX/licenses/$pkgname 

} 
