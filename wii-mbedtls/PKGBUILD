# Maintainer: rw-r-r-0644 <rwrr0644@gmail.com>
# Contributors: WinterMute <davem@devkitpro.org>, Elouan Martinet <exa@elou.world>

pkgname=wii-mbedtls
pkgver=3.6.2
pkgrel=2
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library (for Nintendo Wii homebrew development)'
arch=('any')
url='https://tls.mbed.org/'
license=('apache')
options=(!strip libtool staticlibs)
depends=(ppc-zlib)
makedepends=('wii-cmake' 'wii-pkg-config' 'libwiisocket')
source=( "https://github.com/Mbed-TLS/mbedtls/releases/download/mbedtls-${pkgver}/mbedtls-${pkgver}.tar.bz2"
         'wii-mbedtls.patch' )

sha256sums=('8b54fb9bcf4d5a7078028e0520acddefb7900b3e66fec7f7175bb5b7d85ccdca'
            'SKIP')

build() {
  cd mbedtls-$pkgver

  patch -Np1 -i $srcdir/wii-mbedtls.patch

  unset CFLAGS CPPFLAGS CXXFLAGS LDFLAGS

  ./scripts/config.pl unset MBEDTLS_NO_PLATFORM_ENTROPY
  ./scripts/config.pl set MBEDTLS_ENTROPY_C
  ./scripts/config.pl set MBEDTLS_TIMING_C
  ./scripts/config.pl set MBEDTLS_CMAC_C
  ./scripts/config.pl unset MBEDTLS_ENTROPY_HARDWARE_ALT
  ./scripts/config.pl unset MBEDTLS_SELF_TEST
  ./scripts/config.pl unset MBEDTLS_TIMING_ALT

  export MACHDEP="-DGEKKO -mrvl -mcpu=750 -meabi -mhard-float"
  export LDFLAGS="${MACHDEP} ${LDFLAGS} -L${DEVKITPRO}/libogc/lib/wii"
  export CPPFLAGS="${MACHDEP} ${CPPFLAGS} -I${DEVKITPRO}/libogc/include"

  cmake -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/cmake/Wii.cmake \
    -DCMAKE_INSTALL_PREFIX="${DEVKITPRO}/portlibs/wii" \
    -DCMAKE_C_FLAGS="${CPPFLAGS}" \
    -DENABLE_TESTING=FALSE -DENABLE_PROGRAMS=FALSE \
    .

  make

}

package() {

  cd mbedtls-$pkgver

  make install DESTDIR="$pkgdir"

  install -d "$pkgdir"$PORTLIBS_PREFIX/licenses/$pkgname
  cp -v LICENSE "$pkgdir"$PORTLIBS_PREFIX/licenses/$pkgname 

} 
